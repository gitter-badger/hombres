apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'jacoco'

group 'org.bitionaire'
version '1.0'

mainClassName = 'org.bitionaire.hombres.HombresApplication'

sourceCompatibility = 1.8
targetCompatibility = 1.8

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

configurations {
    provided
    compile.extendsFrom provided
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'io.dropwizard', name: 'dropwizard-core', version: '0.9.1'
    compile group: 'io.dropwizard', name: 'dropwizard-jdbi', version: '0.9.1'

    compile group: 'org.glassfish.jersey.ext', name: 'jersey-declarative-linking', version: '2.17'
    compile group: 'javax.el', name: 'javax.el-api', version: '2.2.4'
    compile group: 'org.glassfish.web', name: 'javax.el', version: '2.2.4'

    compile group: 'org.flywaydb', name: 'flyway-core', version: '3.2.1'
    compile group: 'org.hsqldb', name: 'hsqldb', version: '2.3.3'

    provided group: 'org.projectlombok', name: 'lombok', version: '1.16.4'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
check.dependsOn jacocoTestReport

run {
    args "server", "${project.projectDir}/src/main/config/dev.yml"
}